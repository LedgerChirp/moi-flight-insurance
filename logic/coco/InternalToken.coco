coco FlightInsurance

// Local constant variables 
const CLAIM_AMOUNT U64 = 100000
const CLAIM_INTERVAL I64 = 3600  // every 1 hour

////////////////////////
// Internal Functions
////////////////////////

// Note: functions can only be called internally within the logic

func transfer!(from Address, to Address, value U64):
    if from == Address(0):
        throw "Invalid Sender Address"
    if to == Address(0):
        throw "Invalid Recipient Address"
    
    mutate balanceOf <- SampleLogic.State.balanceOf:
        if balanceOf[from] < value:
            throw "Insufficient Balance"
        
        balanceOf[from] -= value
        balanceOf[to] += value


func mint!(account Address, value U64):
    if account == Address(0):
        throw "Invalid Receiver Address"

    mutate totalSupply <- SampleLogic.State.totalSupply:
        totalSupply += value

    mutate balanceOf <- SampleLogic.State.balanceOf:
        balanceOf[account] += value


func burn!(account Address, value U64):
    if account == Address(0):
        throw "Invalid Sender Address"

    mutate totalSupply <- SampleLogic.State.totalSupply:
        totalSupply -= value

    mutate balanceOf <- SampleLogic.State.balanceOf:
        balanceOf[account] -= value


////////////////////////
// Mutate/Write Endpoints
///////////////////////

// Note: All Endpoints can only be called externally and not within the logic

endpoint invokable Transfer!(to Address, value U64):
    transfer!(from: Address(Sender), to, value)


endpoint invokable Approve!(spender Address, value U64):
    if spender == Address(0):
        throw "Invalid Spender Address"

    var owner = Address(Sender)

    mutate allowances <- SampleLogic.State.allowances:
        allowances[owner][spender] = value


endpoint invokable TransferFrom!(from Address, to Address, value U64):
    var spender = Address(Sender)
    
    mutate allowances <- SampleLogic.State.allowances:
        if allowances[from][spender] < value:
            throw "Insufficient allowances"

        allowances[from][spender] -= value

    transfer!(from, to, value)

endpoint invokable Claim!():
    var account = Address(Sender)
    observe nextClaims <- SampleLogic.State.nextClaims:
        if nextClaims[account] > Env.Timestamp():
            throw "Next Claim at {nextClaims[address]}"

    mint!(account, value: CLAIM_AMOUNT)

    mutate nextClaims <- SampleLogic.State.nextClaims:
        nextClaims[account] = Env.Timestamp() + CLAIM_INTERVAL


////////////////////////
// Observe/Read Endpoints
///////////////////////

endpoint invokable Name()->(name String):
    observe name <- SampleLogic.State.name

endpoint invokable Symbol()->(symbol String):
    observe symbol <- SampleLogic.State.symbol

endpoint invokable Decimals()->(decimals U64):
    observe decimals <- SampleLogic.State.decimals

endpoint invokable TotalSupply()->(totalSupply U64):
    observe totalSupply <- SampleLogic.State.totalSupply

endpoint invokable ClaimAmount()->(claimAmount U64):
    yield claimAmount CLAIM_AMOUNT

endpoint invokable ClaimInterval()->(claimInterval I64):
    yield claimInterval CLAIM_INTERVAL

endpoint invokable NextClaim(account Address)->(nextClaim I64):
    observe nextClaims <- SampleLogic.State.nextClaims:
        yield nextClaim nextClaims[account]

endpoint invokable BalanceOf(account Address)->(balance U64):
    observe balanceOf <- SampleLogic.State.balanceOf:
        yield balance balanceOf[account]

endpoint invokable Allowances(owner Address, spender Address)->(allowance U64):
    observe allowances <- SampleLogic.State.allowances:
        yield allowance allowances[owner][spender]

////////////////////////
// Note: This file of FlightInsurance should be strictly used only
// for Internal Token related logic but not the token state variables. 
// As the logic module can only declare state persistent once
// The global module name is still FlightInsurance and will be seen
// a one logic ... this implementation of splitting token logic and app logic
// is only for modularity
////////////////////////